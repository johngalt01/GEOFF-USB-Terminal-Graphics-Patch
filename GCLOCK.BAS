10 ' Make a Analog Clock For GEOFF Terminal 3/01/2025 By John Galt
15 ' SETUP THE GRAPHICS FUNCTIONS FOR THE ANSI TERMINAL
20 DEF FNLTRIM$ (X$)= RIGHT$(X$, (LEN(X$)-1)) 'STRIP THE LEADING SPACE STR CONVERT
30 DEF FNDRAWLON$ (X1$,Y1$,X2$,Y2$) = CHR$(27)+"[Z1;"+X1$+";"+Y1$+";"+X2$+";"+Y2$+"Z" ' LINE ON (X1,Y1,X2,Y2)
35 DEF FNDRAWLOFF$ (X1$,Y1$,X2$,Y2$)= CHR$(27)+"[Z6;"+X1$+";"+Y1$+";"+X2$+";"+Y2$+"Z" ' LINE OFF (X1,Y1,X2,Y2)
40 DEF FNELLIPSEON$  (X$,Y$,R$)= CHR$(27)+"[Z4;"+X$+";"+Y$+";"+R$+"Z" ' CIRCLE ON (X1,Y1,R)
45 DEF FNELLIPSEOFF$ (X$,Y$,R$)= CHR$(27)+"[Z9;"+X$+";"+Y$+";"+R$+"Z" ' CIRCLE OFF (X1,Y1,R)
50 DEF FNFILLBOXOFF$ (X1$,Y1$,X2$,Y2$)= CHR$(27)+"[Z8;"+X1$+";"+Y1$+";"+X2$+";"+Y2$+"Z" ' FILLBOX OFF(X1,Y1,X2,Y2)
55 DEF FNLOATE$ (X$,Y$)= CHR$(27)+"["+X$+";"+Y$+"H" 'LOCATE TEXT ON TERMINAL AT ROW,COL    
60 DEF FNCLR$ = CHR$(27)+"[2J"
65 DEF FNCURSORON$ = CHR$(27)+"[?25h"
70 DEF FNCURSOROFF$ = CHR$(27)+"[?25l"
75 DEF FNCLRTERM$ = CHR$(27)+"[2J"
80 HOME$=CHR$(27)+"[H"
95 CLS$=CHR$(27)+"[2J"
180 PRINT CLS$;FNCURSOROFF$
190 O=0:PIE=3.141592:PM=0
195 ' DRAW THE CLOCK FACE WITH BORDER AND TICKS.
210 X%=240
220 Y%=216
230 R%=90
250 PRINT FNELLIPSEON$(FNLTRIM$(STR$(X%)),FNLTRIM$(STR$(Y%)),FNLTRIM$(STR$(R%)));HOME$;
310 X%=240
320 Y%=216
330 R%=85
360 PRINT FNELLIPSEON$(FNLTRIM$(STR$(X%)),FNLTRIM$(STR$(Y%)),FNLTRIM$(STR$(R%)));HOME$;
400 FOR I = 0 TO 60
410 X = COS(I * pie / 30) * 96
420 Y = SIN(I * pie / 30) * 85
430 ass = I MOD 5 = 0
440 X1 = X * (.95 + ass * .1)
450 Y1 = Y * (.95 + ass * .1)
470 X1%=X + 240
480 Y1%=Y + 216
490 X2%=X1 + 240
500 Y2%=Y1 + 216
520 PRINT FNDRAWLON$(FNLTRIM$(STR$(X1%)),FNLTRIM$(STR$(Y1%)),FNLTRIM$(STR$(X2%)),FNLTRIM$(STR$(Y2%)));HOME$;
530 NEXT
550 ' MAIN LOOP START
600 WHILE EXITPROG$=""
601 EXITPROG$=INKEY$
620 GOSUB 7000 ' READ TIME
621 'check the date
622 IF ((PREVMONTH<>MONTH) OR (PREVDAY<>DAY) OR (PREVYEAR<>YEAR) OR (PREVWEEKDAY<>WEEKDAY)) THEN GOSUB 9000
775 ' DRAW THE SECONDS HAND
780 IF (O <> S) THEN GOTO 5000
790 IF (O=S) THEN GOTO 6000
800 X = COS(r) * 79
810 Y = SIN(r) * 70
830 X1%=240
840 Y1%=216
850 X2%=X+240
860 Y2%=Y+216
866 PRINT HOME$;FNDRAWLOFF$(FNLTRIM$(STR$(PREVSECX1%)),FNLTRIM$(STR$(PREVSECY1%)),FNLTRIM$(STR$(PREVSECX2%)),FNLTRIM$(STR$(PREVSECY2%)));HOME$; 'ERASE
880 PRINT HOME$;FNDRAWLON$(FNLTRIM$(STR$(X1%)),FNLTRIM$(STR$(Y1%)),FNLTRIM$(STR$(X2%)),FNLTRIM$(STR$(Y2%)));HOME$; 'REDRAW
882 PREVSECX1%=X1%
883 PREVSECY1%=Y1%
884 PREVSECX2%=X2%
885 PREVSECY2%=Y2%
886'DRAW THE MINUTES HAND
890 x = COS((M * 2 + S / 30 - 30) * pie / 60) * 69
900 y = SIN((M * 2 + S / 30 - 30) * pie / 60) * 60
920 X1%=240
930 Y1%=216
940 X2%=X+240
950 Y2%=Y+216
969 PRINT HOME$;FNDRAWLOFF$(FNLTRIM$(STR$(PREVMINX1%)),FNLTRIM$(STR$(PREVMINY1%)),FNLTRIM$(STR$(PREVMINX2%)),FNLTRIM$(STR$(PREVMINY2%)));HOME$; 'ERASE
970 PRINT HOME$;FNDRAWLON$(FNLTRIM$(STR$(X1%)),FNLTRIM$(STR$(Y1%)),FNLTRIM$(STR$(X2%)),FNLTRIM$(STR$(Y2%)));HOME$; 'REDRAW
976 PREVMINX1%=X1%
977 PREVMINY1%=Y1%
978 PREVMINX2%=X2%
979 PREVMINY2%=Y2%
980 ' DRAW THE HOURS HAND
982 x = COS((H * 10 + M * 4 / 30 - 30) * pie / 60) * 49
990 y = SIN((H * 10 + M * 4 / 30 - 30) * pie / 60) * 40
1010 X1%=240
1020 Y1%=216
1030 X2%=X+240
1040 Y2%=Y+216
1055 PRINT HOME$;FNDRAWLOFF$(FNLTRIM$(STR$(PREVHORX1%)),FNLTRIM$(STR$(PREVHORY1%)),FNLTRIM$(STR$(PREVHORX2%)),FNLTRIM$(STR$(PREVHORY2%)));HOME$; 'ERASE
1060 PRINT HOME$;FNDRAWLON$(FNLTRIM$(STR$(X1%)),FNLTRIM$(STR$(Y1%)),FNLTRIM$(STR$(X2%)),FNLTRIM$(STR$(Y2%)));HOME$; 'REDRAW
1071 PREVHORX1%=X1%
1072 PREVHORY1%=Y1%
1073 PREVHORX2%=X2%
1074 PREVHORY2%=Y2%
1080 ' REDRAW THE CENTER HUB MOVE THIS TO AFTER THE NEW HOURS, MINUTES, SECONDS IS REDRAWN
1100 X%=240
1200 Y%=216
1210 R%=5
1239 PRINT HOME$;FNELLIPSEON$(FNLTRIM$(STR$(X%)),FNLTRIM$(STR$(Y%)),FNLTRIM$(STR$(R%)));HOME$; 'REDRAW CENTER CIRCLE
1241 PREVMONTH=MONTH
1242 PREVDAY=DAY
1243 PREVYEAR=YEAR
1244 PREVWEEKDAY=WEEKDAY
1270 WEND
1280 PRINT FNCLR$;FNCURSORON$;HOME$;
1300 END
5000 'ADJUST THE ARC OF THE SECONDS HAND TICK AND MINUTES AND HOURS
5010 O=S
5100 R=(S-15)*PIE/30
5150 DD=1
5200 GOTO 800
6000 R=R+.01
6050 DD=0
6100 GOTO 800
7000 ' READ THE TIME FUNCTION
7001 ' READ SECONDS
7010 OUT 96,129
7020 IF INP(96) AND 128 THEN 7020
7025 D=INP(97)
7030 D=INT(D/16)*10+(D AND 15)
7040 S=D
7050 'READ MINUTES
7060 OUT 96,131
7070 IF INP(96) AND 128 THEN 7070
7075 D=INP(97)
7080 D=INT(D/16)*10+(D AND 15)
7090 M=D
7100 'READ HOURS
7110 OUT 96,133
7120 IF INP(96) AND 128 THEN 7120
7125 D=INP(97)
7130 D=INT(D/16)*10+(D AND 15)
7140 H=D
7150 IF (H>12) THEN H=H-12:PM=1
7160 IF (H=12) THEN PM=1
7170 IF (H=0) THEN H=12
7171 ' READ SECONDS AGAIN ROLL OVER CHECK
7172 OUT 96,129
7173 IF INP(96) AND 128 THEN 7173
7174 D=INP(97)
7175 D=INT(D/16)*10+(D AND 15)
7176 IF D<S THEN GOTO 7000
7177 'READ DAY
7178 OUT 96,135
7179 IF INP(96) AND 128 THEN 7179
7180 D=INP(97)
7181 D=INT(D/16)*10+(D AND 15)
7182 DAY=D
7183 ' READ MONTH
7190 OUT 96,137
7200 IF INP(96) AND 128 THEN 7200
7205 D=INP(97)
7210 D=INT(D/16)*10+(D AND 15)
7220 MONTH=D
7230 'READ WEEKDAY
7240 OUT 96,139
7250 IF INP(96) AND 128 THEN 7250
7255 D=INP(97)
7260 D=INT(D/16)*10+(D AND 15)
7270 WEEKDAY=D
7280 IF WEEKDAY=1 THEN WEEKDAY$="Sunday"
7290 IF WEEKDAY=2 THEN WEEKDAY$="Monday"
7300 IF WEEKDAY=3 THEN WEEKDAY$="Tuesday"
7310 IF WEEKDAY=4 THEN WEEKDAY$="Wednesday"
7320 IF WEEKDAY=5 THEN WEEKDAY$="Thursday"
7330 IF WEEKDAY=6 THEN WEEKDAY$="Friday"
7340 IF WEEKDAY=7 THEN WEEKDAY$="Saturday"
7350 'READ YEAR
7360 OUT 96,141
7365 IF INP(96) AND 128 THEN 7365
7370 D=INP(97)
7380 D=INT(D/16)*10+(D AND 15)
7390 YEAR=D
7400 RETURN
9000 ' TODAY DISPLAY INFORMATION
9010 TX1%=10:TY1%=10:TX2%=500:TY2%=50
9020 PRINT HOME$;FNFILLBOXOFF$ (FNLTRIM$(STR$(TX1%)),FNLTRIM$(STR$(TY1%)),FNLTRIM$(STR$(TX2%)),FNLTRIM$(STR$(TY2%)));
9040 TX1%=1:TY1%=25:PRINT HOME$;FNLOATE$(FNLTRIM$(STR$(TX1%)),FNLTRIM$(STR$(TY1%)))+"The current date is: "WEEKDAY$" "MONTH"/"DAY"/"YEAR;HOME$;
9100 RETURN

